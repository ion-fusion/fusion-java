// Copyright (c) 2012-2013 Amazon.com, Inc.  All rights reserved.

(module defines (quote '#%kernel')

  (use '/fusion/syntax')

  (provide defpub defpub_j defpub_syntax defpub_j_syntax)

  (define_syntax defpub  // WARNING: Doc syntax isn't supported yet.
    '''
    (defpub name value)

Defines and provides a transformer binding for ID to the VALUE.
    '''
    (lambda (stx)
      (quasisyntax
        (begin
          (unsyntax (syntax_cons_q define (syntax_subseq stx 1)))
          (provide (unsyntax (syntax_get stx 1)))))))


  (define_syntax defpub_j  // WARNING: Doc syntax isn't supported yet.
    '''
    (defpub_j id fqcn)

Defines and provides a binding for ID, using `java_new` and the given
fully-qualified class name string FQCN.
    '''
    (lambda (stx)
      (if (= 4 (syntax_size stx))
        (if (is_string (syntax_to_datum (syntax_get stx 2)))
          // We have docs
          (quasisyntax
            (defpub (unsyntax (syntax_get stx 1))
              (unsyntax (syntax_get stx 2))
              (java_new (unsyntax (syntax_get stx 3)))))
          (wrong_syntax (syntax_get stx 2) "Expected documentation string"))
        (quasisyntax
          (defpub (unsyntax (syntax_get stx 1))
            (java_new (unsyntax (syntax_get stx 2))))))))


  (define_syntax defpub_syntax  // WARNING: Doc syntax isn't supported yet.
    '''
    (defpub_syntax name value)

Defines and provides a transformer binding for ID to the VALUE (which must
generally be a transform procedure).
    '''
    (lambda (stx)
      (quasisyntax
        (begin
          (unsyntax (syntax_cons_q define_syntax (syntax_subseq stx 1)))
          (provide (unsyntax (syntax_get stx 1)))))))


  (define_syntax defpub_j_syntax  // WARNING: Doc syntax isn't supported yet.
    '''
    (defpub_j_syntax_j id fqcn)

Defines and provides a transformer binding for ID, using `java_new` and the
given fully-qualified class name string FQCN.
    '''
    (lambda (stx)
      (quasisyntax
        (defpub_syntax (unsyntax (syntax_get stx 1))
          (java_new (unsyntax (syntax_get stx 2)))))))
)
