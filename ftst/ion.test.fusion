// Copyright (c) 2012-2014 Amazon.com, Inc.  All rights reserved.

(require
  "/check"
  "/fusion/experimental/syntax"
  "/fusion/private/builtins")  // annotate


//==========================================================================
// annotate and type_annotations

// TODO what about mutable variants?
//   These tests apply `annotate` to quoted (therefore immutable) data, but
//   mutable types may have different implementations.

(define (checked_annotate datum annotations)
  (let [(annotated_datum (apply annotate datum annotations))]
    (check_annotations annotated_datum annotations)
    annotated_datum))


(define_syntax check_annotate1
  (lambda (stx)
    (lets [(orig_datum_stx (syntax_get stx 1)),
           (datum          (syntax_to_datum orig_datum_stx)),
           (    datum_stx (datum_to_syntax (checked_annotate datum [        ]))),
           (  a_datum_stx (datum_to_syntax (checked_annotate datum ["a"     ]))),
           (a_a_datum_stx (datum_to_syntax (checked_annotate datum ["a", "a"]))),
           (a_b_datum_stx (datum_to_syntax (checked_annotate datum ["a", "b"])))]
      (quasisyntax
        (begin
          (check ===
            (annotate (quote (unsyntax orig_datum_stx)))
            (quote (unsyntax datum_stx)))
          (check ===
            (annotate (quote (unsyntax orig_datum_stx)) "a")
            (quote (unsyntax a_datum_stx)))
          (check ===
            (annotate (quote (unsyntax orig_datum_stx)) "a" "a")
            (quote (unsyntax a_a_datum_stx)))
          (check ===
            (annotate (quote (unsyntax orig_datum_stx)) "a" "b")
            (quote (unsyntax a_b_datum_stx))))))))

(define_syntax check_annotate
  (lambda (stx)
    (lets [(datum_stx (syntax_get stx 1)),
           (datum     (syntax_to_datum datum_stx)),
           (  a_datum_stx (datum_to_syntax (checked_annotate datum ["a"     ]))),
           (a_a_datum_stx (datum_to_syntax (checked_annotate datum ["a", "a"]))),
           (a_b_datum_stx (datum_to_syntax (checked_annotate datum ["a", "b"])))]
      (quasisyntax
        (begin
          (check === (type_annotations (quote (unsyntax datum_stx))) [])
          (check_annotate1 (unsyntax     datum_stx))
          (check_annotate1 (unsyntax   a_datum_stx))
          (check_annotate1 (unsyntax a_a_datum_stx))
          (check_annotate1 (unsyntax a_b_datum_stx)))))))

(check_representative_ion_data check_annotate)


(check_arg_exn (annotate  void  "a"))  // procedure isn't annotatable
(check_arg_exn (annotate (void) "a"))  // void      isn't annotatable


//==========================================================================
// is_null_null

(check_true (is_null_null null.null))
(check_true (is_null_null (quote ann::null.null)))

(check_false (is_null_null (void)))
(check_false (is_null_null null.struct))
(check_false (is_null_null (quote null.symbol)))


"PASSED" // Helpful output if you run this stand-alone
