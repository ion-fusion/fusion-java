// Copyright Ion Fusion contributors. All rights reserved.
// SPDX-License-Identifier: Apache-2.0

(require
  "/fusion/experimental/check"
  "/fusion/experimental/syntax"
  (rename_in "/fusion/experimental/syntax" (quote_syntax QS)))


// Very basic macro
(define_syntax S1
  (lambda (stx) (quote_syntax 99)))

(check === 99 (S1))


// Test use of macro as body of define_syntax
(define_syntax S2
  (let ((xform (lambda (stx) (quote_syntax 99))))
    xform))

(check === 99 (S2))


// (D v i) => (define i (lambda () v))
(define_syntax D
  (lambda (stx)
    (quasisyntax
      (define (unsyntax (syntax_get stx 2))
        (lambda () (unsyntax (syntax_get stx 1)))))))

(D 67 x)
(check === 67 (x))


// Macro inside of unsyntax
(define_syntax D2
  (lambda (stx)
    (quasisyntax
      ( + 2 (unsyntax (let ((x (quote_syntax 220)))
                        (quasisyntax (unsyntax x))))))))
(check === 222 (D2))


// Macro with tail call
(define s3
  (lambda () (quote_syntax 99)))
(define_syntax S3
  (lambda (stx) (apply s3 [])))
(check === 99 (S3))


//============================================================================
// identifier bindings

(define_syntax fie
  (lambda (stx)
    (let ((id1 (syntax_get stx 1))
          (id2 (syntax_get stx 2)))
      (if (free_identifier_equal id1 id2)
        (quote_syntax true)
        (quote_syntax false)))))
(check_true  (fie < <) )
(check_false (fie < >))
(check_true  (fie unbound unbound))

// Generate two identifiers with the same name but different marks.
// They are not free_identifier_equal because of those marks.
(define_syntax make_id
  (lambda (stx)
    (quote_syntax
      (let ((id "dummy"))       // Each use of make_id makes a fresh id
        (quote_syntax id)))))
(define_syntax compare_diff_ids
  (lambda (stx)
    (let ((id1 (make_id))
          (id2 (make_id)))
      (quasisyntax (fie (unsyntax id1) (unsyntax id2))))))
(check_false (compare_diff_ids))


// Test cases taken from Racket docs for `bound-identifier=?`
// https://docs.racket-lang.org/reference/stxcmp.html#(def._((quote._~23~25kernel)._bound-identifier~3d~3f))

(define_syntax check_bound
  (lambda (stx)
    (let [(x (syntax_get stx 1)),
          (y (syntax_get stx 2))]
      (if (bound_identifier_equal x y)
        (quasisyntax (let [((unsyntax y) "wrong")]
                       (let [((unsyntax x) "binds")]
                         (unsyntax y))))
        (quasisyntax (let [((unsyntax y) "no_binds")]
                       (let [((unsyntax x) "wrong")]
                         (unsyntax y))))))))

(check === "binds"    (check_bound a a))
(check === "no_binds" (check_bound a b))
(check === "binds"    (check_bound < <))
(check === "no_binds" (check_bound < >))

(define_syntax check_a
  (lambda (stx)
    (let [(x (syntax_get stx 1))]
      (quasisyntax (check_bound a (unsyntax x))))))

(check === "no_binds" (check_a a))


//============================================================================
// check_duplicate_identifier

(define ID_A (QS a))
(define ID_B (QS b))
(define ID_C (QS c))

(define_syntax make_a
  '''
Expand to an identifier `a` that's not `bound_identifier_equal` to any other,
by virtue of being macro-introduced.
  '''
  (lambda (stx)
    (QS (QS a))))

(check_void (check_duplicate_identifier (quote null.sexp)))
(check_void (check_duplicate_identifier (sexp)))
(check_void (check_duplicate_identifier (sexp ID_A)))
(check_void (check_duplicate_identifier (sexp ID_A ID_B)))
(check_void (check_duplicate_identifier (sexp ID_A ID_B ID_C)))
(check_void (check_duplicate_identifier (sexp ID_A (make_a))))
(check_void (check_duplicate_identifier (sexp (make_a) (make_a))))

// Return the first duplicate id (ie, the first with a preceding match).
(check_same ID_A (check_duplicate_identifier (sexp ID_A   ID_A)))
(check_same ID_A (check_duplicate_identifier (sexp (QS a) ID_A)))
(check_same ID_A (check_duplicate_identifier (sexp (QS a) ID_A   ID_B ID_B)))
(check_same ID_A (check_duplicate_identifier (sexp ID_B   (QS a) ID_A ID_B)))

(expect_argument_error (check_duplicate_identifier []))
(expect_argument_error (check_duplicate_identifier [ID_A]))
(expect_argument_error (check_duplicate_identifier (sexp (quote a))))

// Elements following a match are checked
(expect_argument_error (check_duplicate_identifier (sexp ID_A ID_A (quote a))))


//============================================================================
// syntax_append

(define_syntax join
  (lambda (stx)
    (syntax_append (syntax_get stx 1) (syntax_get stx 2))))

(check === 16
  (let ((x 3)) (join (+ 10 x) (x))))


//============================================================================
// syntax_subseq

(define_syntax from1
  (lambda (stx)
    (syntax_subseq (syntax_get stx 1) 1)))

(check === 5
  (from1 (ignored + 2 3)) 5)

(define_syntax prepend_from1
  (lambda (stx)
    (syntax_append (quasisyntax (+ 1)) (syntax_subseq (syntax_get stx 1) 1))))

// Test syntax_subseq with index over size
(check === 4 (prepend_from1 (2 3)))
(check === 1 (prepend_from1 (2  )))
(check === 1 (prepend_from1 (   )))


//============================================================================
// Hygiene tests

// Macro must not capture bindings from outside.

(define_syntax dont_capture
  (lambda (stx)
    (quasisyntax
      (let ((x 9))
        (unsyntax (syntax_get stx 1))))))

(check === 3
  (let ((x 3)) (dont_capture x)))


// Macro use must not capture bindings from inside the macro.

(define_syntax or2 // 2-part or
  (lambda (stx)
    (let ((e1 (syntax_get stx 1))
          (e2 (syntax_get stx 2)))
      (quasisyntax
        (let ((t (unsyntax e1))) (if t t (unsyntax e2)))))))

(check_true (let ((t true) (if false)) (or2 if t)))


// Macro-defining macros require multiple syntax marks to maintain hygiene.

(define_syntax make_plus_N  // usage: (make_plus_N name n)
  (lambda (stx)
    (let ((name (syntax_get stx 1))
          (n    (syntax_get stx 2)))
      (quasisyntax
        (define_syntax (unsyntax name)  // usage: (name init)
          (lambda (stx1)
            (let ((init (syntax_get stx1 1)))
              (quasisyntax
                (let ((x (unsyntax init)))
                  (+ x (unsyntax (quote_syntax (unsyntax n)))))))))))))

(make_plus_N plus10 10)

(check === 13 (plus10 3))


//============================================================================
// Breaking hygiene via datum_to_syntax

(define_syntax capture  // capture use of 'c'
  (lambda (stx)
    (lets [(arg (syntax_get stx 1)),
           // TODO Use entire arg, not first elt, as context
           //  https://github.com/ion-fusion/fusion-java/issues/68
           (ctx (syntax_get arg 0)),
           (c   (datum_to_syntax (quote c) ctx))]
      (quasisyntax
        (let [((unsyntax c) 2)]
          (unsyntax (datum_to_syntax arg (quote_syntax here))))))))

(check === 20
  (let ((c 5))
    (capture (* 10 c))))


"PASSED" // Helpful output if you run this stand-alone
